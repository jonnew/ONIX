* OPA170 - Rev. A
* Created by Ian Williams; January 18, 2017
* Created with Green-Williams-Lis Op Amp Macro-model Architecture
* Copyright 2017 by Texas Instruments Corporation
******************************************************
* MACRO-MODEL SIMULATED PARAMETERS:
******************************************************
* OPEN-LOOP GAIN AND PHASE VS. FREQUENCY  WITH RL, CL EFFECTS (Aol)
* UNITY GAIN BANDWIDTH (GBW)
* INPUT COMMON-MODE REJECTION RATIO VS. FREQUENCY (CMRR)
* POWER SUPPLY REJECTION RATIO VS. FREQUENCY (PSRR)
* DIFFERENTIAL INPUT IMPEDANCE (Zid)
* COMMON-MODE INPUT IMPEDANCE (Zic)
* OPEN-LOOP OUTPUT IMPEDANCE VS. FREQUENCY (Zo)
* OUTPUT CURRENT THROUGH THE SUPPLY (Iout)
* INPUT VOLTAGE NOISE DENSITY VS. FREQUENCY (en)
* INPUT CURRENT NOISE DENSITY VS. FREQUENCY (in)
* OUTPUT VOLTAGE SWING vs. OUTPUT CURRENT (Vo)
* SHORT-CIRCUIT OUTPUT CURRENT (Isc)
* QUIESCENT CURRENT (Iq)
* SETTLING TIME VS. CAPACITIVE LOAD (ts)
* SLEW RATE (SR)
* SMALL SIGNAL OVERSHOOT VS. CAPACITIVE LOAD
* LARGE SIGNAL RESPONSE
* OVERLOAD RECOVERY TIME (tor)
* INPUT BIAS CURRENT (Ib)
* INPUT OFFSET CURRENT (Ios)
* INPUT OFFSET VOLTAGE (Vos)
* INPUT COMMON-MODE VOLTAGE RANGE (Vcm)
* INPUT OFFSET VOLTAGE vs. INPUT COMMON-MODE VOLTAGE (Vos vs. Vcm)
* INPUT/OUTPUT ESD CELLS (ESDin, ESDout)
******************************************************
.subckt OPA170 IN+ IN- VCC VEE OUT
******************************************************
* MODEL DEFINITIONS:
.model BB_SW VSWITCH(Ron=50 Roff=1e9 Von=700e-3 Voff=0)
.model ESD_SW VSWITCH(Ron=50 Roff=1e9 Von=500e-3 Voff=100e-3)
.model OL_SW VSWITCH(Ron=1e-3 Roff=1e9 Von=900e-3 Voff=800e-3)
.model OR_SW VSWITCH(Ron=10e-3 Roff=1e9 Von=1e-3 Voff=0)
.model R_NOISELESS RES(T_ABS=-273.15)
******************************************************
V_OS N047 en_p 210.996e-6
R1 N041 N039 R_NOISELESS 1e-3 
R2 N055 ESDn R_NOISELESS 1e-3 
R3 N074 0 R_NOISELESS 1e9 
C1 N074 0 1
R4 VCC_B N073 R_NOISELESS 1e-3 
C2 N073 0 1e-15
C3 N075 0 1e-15
R5 N075 VEE_B R_NOISELESS 1e-3 
G1 N041 N042 N005 N014 1e-3
R6 MID N051 R_NOISELESS 1e9 
VCM_MIN N054 VEE_B -0.1
R7 N054 MID R_NOISELESS 1e9 
VCM_MAX N051 VCC_B -2
XVCM_CLAMP N042 MID N048 MID N051 N054 VCCS_EXT_LIM
R8 N048 MID R_NOISELESS 1 
C4 N049 MID 1e-15
R9 N048 N049 R_NOISELESS 1e-3 
V4 N070 OUT 0
R10 MID N058 R_NOISELESS 1e9 
R11 MID N059 R_NOISELESS 1e9 
XIQ+ VIMON MID VCC MID VCCS_LIM_IQ
XIQ- MID VIMON VEE MID VCCS_LIM_IQ
R12 VCC_B N015 R_NOISELESS 1e3 
R13 N028 VEE_B R_NOISELESS 1e3 
XCLAWp VIMON MID N015 VCC_B VCCS_LIM_CLAWp
XCLAWn MID VIMON VEE_B N028 VCCS_LIM_CLAWn
R14 VEE_CLP MID R_NOISELESS 1e3 
R15 MID VCC_CLP R_NOISELESS 1e3 
R16 N016 N015 R_NOISELESS 1e-3 
R17 N029 N028 R_NOISELESS 1e-3 
C5 MID N016 1e-15
C6 N029 MID 1e-15
R18 VOUT_S N059 R_NOISELESS 100 
C7 VOUT_S MID 1e-9
G2 MID VCC_CLP N016 MID 1e-3
G3 MID VEE_CLP N029 MID 1e-3
XCL_AMP N011 N040 VIMON MID N019 N026 CLAMP_AMP_LO
V_ISCp N011 MID 17
V_ISCn N040 MID -20
XOL_SENSE MID N046 N045 N053 OL_SENSE
R19 N040 MID R_NOISELESS 1e9 
R20 N026 MID R_NOISELESS 1 
C8 N027 MID 1e-15
R21 MID N019 R_NOISELESS 1 
R22 MID N011 R_NOISELESS 1e9 
C9 MID N020 1e-15
XCLAW_AMP VCC_CLP VEE_CLP VOUT_S MID N017 N024 CLAMP_AMP_LO
R23 VEE_CLP MID R_NOISELESS 1e9 
R24 N024 MID R_NOISELESS 1 
C10 N025 MID 1e-15
R25 MID N017 R_NOISELESS 1 
R26 MID VCC_CLP R_NOISELESS 1e9 
C11 MID N018 1e-15
XCL_SRC N020 N027 CL_CLAMP MID VCCS_LIM_4
XCLAW_SRC N018 N025 CLAW_CLAMP MID VCCS_LIM_3
R27 N017 N018 R_NOISELESS 1e-3 
R28 N025 N024 R_NOISELESS 1e-3 
R29 N019 N020 R_NOISELESS 1e-3 
R30 N027 N026 R_NOISELESS 1e-3 
R31 N046 MID R_NOISELESS 1 
R32 N046 SW_OL R_NOISELESS 100 
C12 SW_OL MID 1e-12
R33 VIMON N058 R_NOISELESS 100 
C13 VIMON MID 1e-9
C_DIFF ESDp ESDn 3e-12
C_CMn ESDn MID 3e-12
C_CMp MID ESDp 3e-12
I_Q VCC VEE 110e-6
I_B N042 MID 8e-12
I_OS N055 MID 4e-12
R34 IN+ ESDp R_NOISELESS 10e-3 
R35 IN- ESDn R_NOISELESS 10e-3 
R36 N036 MID R_NOISELESS 1 
R37 N043 MID R_NOISELESS 1e9 
R38 MID N022 R_NOISELESS 1 
R39 MID N012 R_NOISELESS 1e9 
XGR_AMP N012 N043 N021 MID N022 N036 CLAMP_AMP_HI
XGR_SRC N023 N037 CLAMP MID VCCS_LIM_GR
C17 MID N023 1e-15
C18 N037 MID 1e-15
V_GRn N043 MID -75
V_GRp N012 MID 70
R40 N022 N023 R_NOISELESS 1e-3 
R41 N037 N036 R_NOISELESS 1e-3 
R42 VSENSE N021 R_NOISELESS 1e-3 
C19 MID N021 1e-15
R43 MID VSENSE R_NOISELESS 1e3 
G5 N038 N039 N010 MID 1e-3
G8 MID CLAW_CLAMP N057 MID 1e-3
R45 MID CLAW_CLAMP R_NOISELESS 1e3 
G9 MID CL_CLAMP CLAW_CLAMP MID 1e-3
R46 MID CL_CLAMP R_NOISELESS 1e3 
R47 N062 VCLP R_NOISELESS 100 
C24 MID VCLP 1e-12
E4 N062 MID CL_CLAMP MID 1
E5 N059 MID OUT MID 1
H1 N058 MID V4 1e3
S1 N061 N060 SW_OL MID OL_SW
R52 MID ESDp R_NOISELESS 1e9 
R53 ESDn MID R_NOISELESS 1e9 
R58 N039 N038 R_NOISELESS 1e3 
R59 N073 N074 R_NOISELESS 1e6 
R60 N074 N075 R_NOISELESS 1e6 
R67 N042 N041 R_NOISELESS 1e3 
G15 MID VSENSE CLAMP MID 1e-3
V_ORp N035 VCLP 0.68
V_ORn N030 VCLP -0.68
V11 N032 N031 0
V12 N033 N034 0
H3 N044 MID V12 1
S6 VCC OUT OUT VCC ESD_SW
S7 OUT VEE VEE OUT ESD_SW
E1 MID 0 N074 0 1
G16 0 VCC_B VCC 0 1
G17 0 VEE_B VEE 0 1
R88 VCC_B 0 R_NOISELESS 1 
R89 VEE_B 0 R_NOISELESS 1 
S8 N033 CLAMP CLAMP N033 OR_SW
S9 CLAMP N032 N032 CLAMP OR_SW
Xi_nn ESDn MID FEMT
Xi_np en_p MID FEMT
XVCCS_LIMIT_1 N049 N055 MID N050 VCCS_LIM_1
XVCCS_LIMIT_2 N050 MID MID CLAMP VCCS_LIM_2
R44 N050 MID R_NOISELESS 1e6 
R68 CLAMP MID R_NOISELESS 1e6 
G7 MID N056 VSENSE MID 1e-6
R69 N056 MID R_NOISELESS 1e6 
G10 MID N031 N030 MID 1
R73 N031 MID R_NOISELESS 1 
G11 MID N034 N035 MID 1
R74 N034 MID R_NOISELESS 1 
H2 N052 MID V11 -1
XVOS_VCM N038 N047 VCC_B VEE_B VOS_SRC
C16 N003 N004 7.764e-12
R56 N003 MID R_NOISELESS 163.6e3 
R57 N003 N004 R_NOISELESS 1e8 
G_adjust2 MID N004 VCC_B MID 612e-6
Rsrc2 N004 MID R_NOISELESS 1 
G14 MID N002 MID N003 1
Rsrc3 N002 MID R_NOISELESS 1 
C21 N001 N002 7.764e-12
R61 N001 N002 R_NOISELESS 1e8 
R62 N001 MID R_NOISELESS 163.6e3 
G18 MID N005 MID N001 612.2
Rsrc6 N005 MID R_NOISELESS 1 
C29 N014 N013 2.04e-9
R63 N014 MID R_NOISELESS 382.3 
R82 N014 N013 R_NOISELESS 1e8 
G19 MID N013 VEE_B MID 262e-3
Rsrc7 N013 MID R_NOISELESS 1 
Rx N070 N069 R_NOISELESS 110e3
Rdummy N070 MID R_NOISELESS 11e3
G20 MID N060 CL_CLAMP N070 1089.11
Rdc2 N060 MID R_NOISELESS 1
R83 N060 N061 R_NOISELESS 1e4
R84 N061 MID R_NOISELESS 134.5
G21 MID N063 N061 MID 75.34
C30 N061 N060 4.36e-5
R85 N063 MID R_NOISELESS 1
R86 N063 N064 R_NOISELESS 4.8e6
R87 N064 N071 R_NOISELESS 1e4
C31 MID N071 3.96e-10
Gb1 MID N065 N064 MID 1
R90 N067 MID R_NOISELESS 1
R91 N067 N068 R_NOISELESS 1e4
R92 N068 MID R_NOISELESS 17.03
C32 N068 N067 9.36e-14
R93 N065 MID R_NOISELESS 1
R94 N065 N066 R_NOISELESS 24.98e3
R95 N066 N072 R_NOISELESS 1e4
C33 MID N072 9.36e-13
Gc1 MID N067 N066 MID 1
C22 CLAMP MID 3.684e-7
C23 N056 MID 9.588e-15
G4 MID N057 N056 MID 1e-6
R50 N057 MID R_NOISELESS 1e6 
C20 N057 MID 9.588e-15
C14 N007 N006 1.447e-12
R48 N007 MID R_NOISELESS 687.9e3 
R49 N007 N006 R_NOISELESS 1e8 
G_adjust MID N006 ESDp MID 190.3e-6
Rsrc1 N006 MID R_NOISELESS 1 
G6 MID N008 MID N007 1
Rsrc4 N008 MID R_NOISELESS 1 
C15 N009 N008 1.447e-12
R54 N009 N008 R_NOISELESS 1e8 
R55 N009 MID R_NOISELESS 687.9e3 
G12 MID N010 MID N009 146.36
Rsrc5 N010 MID R_NOISELESS 1 
Xe_n N076 MID VNSE
G13 MID N077 N076 N078 1
R64 N077 N078 R_NOISELESS 11.786e3 
R65 N078 N079 R_NOISELESS 11e3 
C25 N079 MID 4e-10
R66 N078 N080 R_NOISELESS 11e3 
C26 N080 N079 5e-11
E6 en_p en_n N077 MID 1
R51 en_n ESDp R_NOISELESS 1e-3 
S2 VCC ESDn ESDn VCC ESD_SW
S3 VCC ESDp ESDp VCC ESD_SW
S4 ESDn VEE VEE ESDn ESD_SW
S5 ESDp VEE VEE ESDp ESD_SW
S10 ESDp ESDn ESDn ESDp BB_SW
S11 ESDn ESDp ESDp ESDn BB_SW
R71 N045 N044 R_NOISELESS 100
R72 N053 N052 R_NOISELESS 100
C27 N045 MID 1e-12
C28 N053 MID 1e-12
Rpd N079 MID R_NOISELESS 1e9 
XZO_SRC N068 MID MID N069 VCCS_LIM_ZO
R70 N069 MID R_NOISELESS 1
.ends OPA170
*
.subckt CLAMP_AMP_HI VC+ VC- VIN COM VO+ VO-
.param G=10
GVo+ COM Vo+ Value = {IF(V(VIN,COM)>V(VC+,COM),((V(VIN,COM)-V(VC+,COM))*G),0)}
GVo- COM Vo- Value = {IF(V(VIN,COM)<V(VC-,COM),((V(VC-,COM)-V(VIN,COM))*G),0)}
.ends CLAMP_AMP_HI
*
.subckt OL_SENSE 1   2  3  4
GSW+ 1 2 Value = {IF((V(3,1)>10e-3 | V(4,1)>10e-3),1,0)}
.ends OL_SENSE
*
.subckt FEMT 1 2
.param FLWF=1e-3
.param GLFF=229.16e-6
.param RNVF=2.664
.model DVNF D KF={PWR(FLWF,0.5)/1E11} IS=1.0e-16
I1 0 7 10e-3
I2 0 8 10e-3
D1 7 0 DVNF
D2 8 0 DVNF
E1 3 6 7 8 {GLFF}
R1 3 0 1e9
R2 3 0 1e9
R3 3 6 1e9
E2 6 4 5 0 10
R4 5 0 {RNVF}
R5 5 0 {RNVF}
R6 3 4 1e9
R7 4 0 1e9
G1 1 2 3 4 1e-6
.ends FEMT
*
.subckt VCCS_EXT_LIM VIN+ VIN- IOUT- IOUT+ VP+ VP-
.param Gain = 1
G1 IOUT+ IOUT- VALUE={LIMIT(Gain*V(VIN+,VIN-),V(VP-,VIN-), V(VP+,VIN-))}
.ends VCCS_EXT_LIM
*
.subckt VCCS_LIM_3 VC+ VC- IOUT+ IOUT-
.param Gain = 1
.param Ipos = 0.14
.param Ineg = -0.15
G1 IOUT+ IOUT- VALUE={LIMIT(Gain*V(VC+,VC-),Ineg,Ipos)}
.ends VCCS_LIM_3
*
.subckt VCCS_LIM_4 VC+ VC- IOUT+ IOUT-
.param Gain = 1
.param Ipos = 0.28
.param Ineg = -0.3
G1 IOUT+ IOUT- VALUE={LIMIT(Gain*V(VC+,VC-),Ineg,Ipos)}
.ends VCCS_LIM_4
*
.subckt VCCS_LIM_CLAWp VC+ VC- IOUT+ IOUT-
G1 IOUT+ IOUT- TABLE {abs(V(VC+,VC-))} =
+(0, 5e-5)
+(3, 3.5e-4)
+(8, 9.4e-4)
+(17.1, 2.2e-3)
.ends VCCS_LIM_CLAWp
*
.subckt VCCS_LIM_CLAWn VC+ VC- IOUT+ IOUT-
G1 IOUT+ IOUT- TABLE {abs(V(VC+,VC-))} =
+(0, 1e-5)
+(4, 2.5e-4)
+(8, 5.1e-4)
+(20.1, 1.4e-3)
.ends VCCS_LIM_CLAWn
*
.subckt VCCS_LIM_IQ VC+ VC- IOUT+ IOUT-
.param Gain = 1e-3
G1 IOUT+ IOUT- VALUE={IF( (V(VC+,VC-)<=0),0,Gain*V(VC+,VC-) )}
.ends VCCS_LIM_IQ
*
.subckt VNSE 1 2
.param FLW=1
.param GLF=164.1e-3
.param RNV=473.6
.model DVN D KF={PWR(FLW,0.5)/1E11} IS=1.0E-16
I1 0 7 10E-3
I2 0 8 10E-3
D1 7 0 DVN
D2 8 0 DVN
E1 3 6 7 8 {GLF}
R1 3 0 1E9
R2 3 0 1E9
R3 3 6 1E9
E2 6 4 5 0 10
R4 5 0 {RNV}
R5 5 0 {RNV}
R6 3 4 1E9
R7 4 0 1E9
E3 1 2 3 4 1
.ends VNSE
*
.subckt CLAMP_AMP_LO VC+ VC- VIN COM VO+ VO-
.param G=1
GVo+ COM Vo+ Value = {IF(V(VIN,COM)>V(VC+,COM),((V(VIN,COM)-V(VC+,COM))*G),0)}
GVo- COM Vo- Value = {IF(V(VIN,COM)<V(VC-,COM),((V(VC-,COM)-V(VIN,COM))*G),0)}
.ends CLAMP_AMP_LO
*
.subckt VCCS_LIM_GR VC+ VC- IOUT+ IOUT-
.param Gain = 1
.param Ipos = 0.3
.param Ineg = -0.3
G1 IOUT+ IOUT- VALUE={LIMIT(Gain*V(VC+,VC-),Ineg,Ipos)}
.ends VCCS_LIM_GR
*
.subckt VCCS_LIM_1 VC+ VC- IOUT+ IOUT-
.param Gain = 1e-4
.param Ipos = .5
.param Ineg = -.5
G1 IOUT+ IOUT- VALUE={LIMIT(Gain*V(VC+,VC-),Ineg,Ipos)}
.ends VCCS_LIM_1
*
.subckt VCCS_LIM_2 VC+ VC- IOUT+ IOUT-
.param Gain =32.27e-3
.param Ipos = 149e-3
.param Ineg = -149e-3
G1 IOUT+ IOUT- VALUE={LIMIT(Gain*V(VC+,VC-),Ineg,Ipos)}
.ends VCCS_LIM_2
*
.subckt VOS_SRC V+ V- REF+ REF-
E1 V+ 1 TABLE {(V(REF+, V-))} =
+(-.1, 4.9e-3)
+(0.84, 4.82e-3)
+(1.2, 3.12e-3)
+(1.56, 0)
+(2, 0)
E2 1 V- TABLE {(V(V-, REF-))}=
+(-0.2, 74.3e-6)
+(4.9, 27.2e-6)
+(6, 20.3e-6 )
+(11, 9.9e-6)
+(18, 3e-6)
+(28, 0)
+(29, 0)
.ends VOS_SRC
*
.subckt VCCS_LIM_ZO VC+ VC- IOUT+ IOUT-
.param Gain = 588.24
.param Ipos = 3.76e3
.param Ineg = -4.4e3
G1 IOUT+ IOUT- VALUE={LIMIT(Gain*V(VC+,VC-),Ineg,Ipos)}
.ends VCCS_LIM_ZO
*